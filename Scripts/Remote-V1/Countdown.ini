; Script Name: Countdown.ini
; Type: Optional
; Credits - None
; You may edit this script.

on *:text:!countdown start *:#: {
  if ($access($nick) <= 400) {
    set $+(%,countdown.,$chan) $3
    if ($4 != $null) {
      writeini -n BotSettings.ini $chan CountdownMessage $4-
    }
    $+(timercountdown.,$chan $calc($3 + 1) 1) countdown $chan
    message $chan $user($nick) -> Timer started.
    if ($BotSetting(CountdownSub)) {
      message $chan /subscribers
    }
  }
}

on *:text:!countdown stop:#: {
  if ($access($nick) <= 400) {
    unset $+(%,countdown.,$chan)
    $+(timercountdown.,$chan off)
    message $chan $user($nick) -> Timer stopped.
  }
}

on *:text:!countdown setting *:#: {
  if ($access($nick) <= 400) {
    if (($3 = colour) || ($3 = color)) {
      if ($4 = on) {
        writeini -n BotSettings.ini $chan CountdownColour ON
        message $chan $user($nick) -> The countdown will now use coloured text.
      }
      if ($4 = off) {
        remini BotSettings.ini $chan CountdownColour
        message $chan $user($nick) -> The countdown will no longer use coloured text.
      }
    }
    if ($3 = submode) {
      if ($4 = on) {
        writeini -n BotSettings.ini $chan CountdownSub ON
        message $chan $user($nick) -> The countdown will now use subscribers only mode.
      }
      if ($4 = off) {
        remini BotSettings.ini $chan CountdownSub
        message $chan $user($nick) -> The countdown will no longer use subscribers only mode.
      }
    }
    if ($3 = message) {
      writeini -n BotSettings.ini $chan DefCountdownMessage $4-
      message $chan $user($nick) -> The default countdown message has been updated.
    }
  }
}


alias Countdown {
  if ($($+(%,countdown.,$1),2) = 0) {
    message $1 $iif($readini(BotSettings.ini,$1,CountdownColour), /me >) $iif($readini(BotSettings.ini, $1, CountdownMessage) != $null, $readini(BotSettings.ini, $1, CountdownMessage), $readini(BotSettings.ini, $1, DefCountdownMessage))
    unset $+(%,countdown.,$1)
    if ($readini(BotSettings.ini, $1, CountdownMessage) != $null) {
      remini BotSettings.ini $1 CountdownMessage
    }
    if ($readini(BotSettings.ini,$1,CountdownSub)) {
      message $1 /subscribersoff
    }
  }
  else {
    message $1 $iif($readini(BotSettings.ini,$1,CountdownColour) != $null, /me >) $($+(%,countdown.,$1),2)
    dec $+(%,countdown.,$1)
  }
}
