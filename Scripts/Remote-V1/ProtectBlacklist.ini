; Script Name: ProtectBlacklist.ini
; Type: Optional
; Credits - REGEX Detector: Source Not Found
; It is recommended that you do not modify this script.

on *:join:#: {
  if (!$istok($($+(%,badwords.,$chan),2),DoNotRemoveMeFromBlacklist,32)) {
    set $+(%,badwords.,$chan) $addtok($($+(%,badwords.,$chan),2),DoNotRemoveMeFromBlacklist,32)
  }
}

on *:text:!set blacklist*:#: {
  if ($access($nick) <= 400) {
    if ($3 = on) {
      writeini -n BotSettings.ini $chan Blacklist ON
      message $chan $user($nick) -> Blacklist protection has been enabled.
    }
    if ($3 = off) {
      remini BotSettings.ini $chan Blacklist
      message $chan $user($nick) -> Blacklist protection has been disabled.
    }
    if ($3 = message) {
      writeini -n BotSettings.ini $chan BlacklistMessage $4-
      message $chan $user($nick) -> Blacklist message updated.
    }
    if ($3 = visibility) {
      if ($4 = on) {
        remini BotSettings.ini $chan BlacklistVisible
        message $chan $user($nick) -> Blacklist messages have been enabled.
      }
      if ($4 = off) {
        writeini -n BotSettings.ini $chan BlacklistVisible .
        message $chan $user($nick) -> Blacklist messages have been disabled.
      }
    }
    if ($3 = shortlength) {
      if (($4 !isnum) || ($4 > $BotSetting(BlacklistLong))) {
        message $chan $user($nick) -> You must choose a number smaller than the long timeout length.
      }
      else {
        writeini -n BotSettings.ini $chan BlacklistShort $4
        message $chan $user($nick) -> The Blacklist short timeout length was updated.
      }
    }
    if ($3 = longlength) {
      if (($4 !isnum) || ($4 < $BotSetting(BlacklistShort))) {
        message $chan $user($nick) -> You must choose a number greater than the short timeout length.
      }
      else {
        writeini -n BotSettings.ini $chan BlacklistLong $4
        message $chan $user($nick) -> The Blacklist long timeout length was updated.
      }
    }
    if ($3 = max) {
      if ($4 = purge) {
        if ($BotSetting(BlacklistMin) = $null) {
          writeini -n BotSettings.ini $chan BlacklistMax Purge
          message $chan $user($nick) -> The Blacklist maximum action was updated.
        }
        else {
          message $chan $user($nick) -> The maximum action cannot be bigger than the minimum.
        }
      }
      if ($4 = short) {
        if ($BotSetting(BlacklistMin) = Ban) || ($BotSetting(BlacklistMin) = Long) {
          message $chan $user($nick) -> The maximum action cannot be bigger than the minimum.
        }
        else {
          remini BotSettings.ini $chan BlacklistMax
          message $chan $user($nick) -> The Blacklist maximum action was updated.
        }
      }
      if ($4 = long) {
        if ($BotSetting(BlacklistMin) = Ban) {
          message $chan $user($nick)-> The maximum action cannot be bigger than the minimum.
        }
        else {
          writeini -n BotSettings.ini $chan BlacklistMax Long
          message $chan $user($nick) -> The Blacklist maximum action was updated.
        }
      }
      if ($4 = ban) {
        writeini -n BotSettings.ini $chan BlacklistMax Ban
        message $chan $user($nick) -> The Blacklist maximum action was updated.
      }
    }
    if ($3 = min) {
      if ($4 = purge) {
        remini BotSettings.ini $chan BlacklistMin
        message $chan $user($nick) -> The Blacklist minimum action was updated.
      }
      if ($4 = short) {
        if ($BotSetting(BlacklistMax) = Purge) {
          message $chan $user($nick) -> The minimum action cannot be bigger than the maximum.
        }
        else {
          writeini -n BotSettings.ini $chan BlacklistMin Short
          message $chan $user($nick) -> The Blacklist minimum action was updated.
        }
      }
      if ($4 = long) {
        if (($BotSetting(BlacklistMax) = Purge) || (($BotSetting(BlacklistMax) = Short)) {
          message $chan $user($nick) -> The minimum action cannot be bigger than the maximum.
        }
        else {
          writeini -n BotSettings.ini $chan BlacklistMin Long
          message $chan $user($nick) -> The Blacklist minimum action was updated.
        }
      }
      if ($4 = ban) {
        if ($BotSetting(BlacklistMax) = Ban) {
          writeini -n BotSettings.ini $chan BlacklistMin Ban
          message $chan $user($nick) -> The Blacklist minimum action was updated.
        }
        else {
          message $chan $user($nick) -> The minimum action cannot be bigger than the maximum.
        }
      }
    }
    if ($3 = warning) {
      if ($4 = on) {
        remini BotSettings.ini $chan BlacklistWarning
        message $chan $user($nick) -> The Blacklist ban warning was enabled.

      }
      if ($4 = off) {
        writeini -n BotSettings.ini $chan BlacklistWarning .
        message $chan $user($nick) -> The Blacklist ban warning was disabled.
      }
    }
    elseif ($3 = allow) {
      if ($4 isnum) && ($4 > 500) {
        writeini -n BotSettings.ini $chan BlacklistSafe $4
        message $chan $user($nick) -> Users with a userlevel of $4 or above will now not be affected.
      }
      else {
        message $chan $user($nick) -> You must choose a userlevel below 500.
      }
    }
    elseif ($3 = $null) {
      message $chan $user($nick) -> Blacklist Protection is currently $iif($BotSetting(Blacklist) != $null,on.,off.)
    }
  }
}

on *:text:!forget blacklist *:#: {
  if ($access($nick) <= 400) {
    unset $+(%,Blacklistban.,$chan,.,$3)
    unset $+(%,Blacklistbanwarning.,$chan,.,$3)
    unset $+(%,Blacklistshorttimeout.,$chan,.,$3)
    unset $+(%,Blacklistlongtimeout.,$chan,.,$3)
    unset $+(%,Blacklistpurge.,$chan,.,$3)
    message $chan $user($nick) -> $3 is no longer known by me on this channel for using blacklisted words.
  }
}

on *:text:!blacklist add*:#: {
  if ($access($nick) <= 400) {
    if ($3 = regex) && ($4) {
      set %regexj 1
      while (%regexj <= $ini(Regex.ini,$chan,0)) {
        if ($readini(Regex.ini,n,$chan,$ini(Regex.ini,$chan,%regexj)) = $4) {
          set %regexk $ini(Regex.ini,$chan,%regexj)
        }
        inc %regexj
      }
      if (!$4) { 
        message $chan $user($nick) -> Please include an expression to be added to the blacklist.
      }
      elseif (%regexk) {
        message $chan $user($nick) -> That expression is already banned.
      }
      else {
        writeini -n Regex.ini $chan $ticks $4
        message $chan $user($nick) -> That expression is now banned.
      }
      unset %regexk
    }
    else {
      if (!$3) { 
        message $chan $user($nick) -> Please include a word to be added to the blacklist.
      }
      elseif ($istok($($+(%,badwords.,$chan),2),$3,32)) { 
        message $chan $user($nick) -> That word is already on the list. 
      }
      else {
        set $+(%,badwords.,$chan) $addtok($($+(%,badwords.,$chan),2),$3,32)
        message $chan $user($nick) -> That word is now banned.
      }
    }
  }
}

on *:text:!blacklist list*:#: {
  if ($access($nick) <= 400) {
    set %regexu 1
    while (%regexu <= $ini(Regex.ini,$chan,0)) {
      set %regexy %regexy $+ $chr(32) $+ $readini(Regex.ini,$chan,$ini(Regex.ini,$chan,%regexu)) $+ %comma
      inc %regexu
    }
    message $chan $user($nick) -> Banned Words: $iif($replace($($+(%,badwords.,$chan),2),DoNotRemoveMeFromBlacklist,$null),$($+(%,badwords.,$chan),2),[None])
    message $chan $user($nick) -> Banned Expressions: $iif(%regexy,%regexy,[None])
    unset %regexy
    unset %regexu
  }
}

on *:text:!blacklist remove*:#: {
  if ($access($nick) <= 400) {
    if ($3 = regex) && ($4) {
      set %regexj 1
      while (%regexj <= $ini(Regex.ini,$chan,0)) {
        if ($readini(Regex.ini,n,$chan,$ini(Regex.ini,$chan,%regexj)) = $4) {
          set %regexk $ini(Regex.ini,$chan,%regexj)
        }
        inc %regexj
      }
      if (!$4) { 
        message $chan $user($nick) -> Please include an expression to be removed the blacklist.
      }
      elseif (!%regexk) {
        message $chan $user($nick) -> That expression is not banned.
      }
      else {
        remini -n Regex.ini $chan %regexk
        message $chan $user($nick) -> That expression is no longer banned.
      }
      unset %regexk
    }
    else {
      if (!$3) { 
        message $chan $user($nick) -> Please include a word to be removed from the blacklist.
      }
      elseif (!$istok($($+(%,badwords.,$chan),2),$3,32)) { 
        message $chan $user($nick) -> That word is not on the list.
      }
      elseif ($3 = DoNotRemoveMeFromBlacklist) {
        message $chan $user($nick) -> This string cannot be removed.
      }
      else {
        set $+(%,badwords.,$chan) $remtok($($+(%,badwords.,$chan),2),$3,1,32)
        message $chan $user($nick) -> That word will no longer be banned.
      }
    }
  }
}

on *:action:*:#:{
  if ($BotSetting(Blacklist) != $null) {
    if ($regex($1-,$+(/(?<![a-z\d])\Q,$replacecs($($+(%,badwords.,$chan),2),\E,\\E\Q,$chr(32),\E|\Q),\E(?![a-z\d])/Si))) {
      ProtectBlacklist $nick
    }
    else {
      set %regexi 1
      while (%regexi <= $ini(Regex.ini,$chan,0)) {
        if ($regex($1-,$readini(Regex.ini,n,$chan,$ini(Regex.ini,$chan,%regexi)))) {
          ProtectBlacklist $nick
        }
        inc %regexi
      }
    }
  }
}

on *:text:*:#:{
  if ($BotSetting(Blacklist) != $null) {
    if ($regex($1-,$+(/(?<![a-z\d])\Q,$replacecs($($+(%,badwords.,$chan),2),\E,\\E\Q,$chr(32),\E|\Q),\E(?![a-z\d])/Si))) {
      ProtectBlacklist $nick
    }
    else {
      set %regexi 1
      while (%regexi <= $ini(Regex.ini,$chan,0)) {
        if ($regex($1-,$readini(Regex.ini,n,$chan,$ini(Regex.ini,$chan,%regexi)))) {
          ProtectBlacklist $nick
        }
        inc %regexi
      }
      unset %regexi
    }
  }
}

alias ProtectBlacklist {
  if ($access($1) <= $iif($BotSetting(BlacklistSafe) != $null,$BotSetting(BlacklistSafe),500)) { return }
  if ($BotSetting(BlacklistWarning) = $null) && ($($+(%,Blacklistlongtimeout.,$chan,.,$1),2)) {
    Blacklistban $1
  }
  elseif ($($+(%,Blacklistlongtimeout.,$chan,.,$1),2)) {
    if ($BotSetting(BlacklistMax) = Long) {
      Blacklistlong $1
    }
    elseif ($BotSetting(BlacklistWarning) = $null) {
      Blacklistwarning $1
    }
    else {
      Blacklistban $1
    }
  }
  elseif ($($+(%,Blacklistshorttimeout.,$chan,.,$1),2)) {
    if ($BotSetting(BlacklistMax) = $null) {
      Blacklistshort $1
    }
    else {
      Blacklistlong $1
    }
  }
  elseif ($($+(%,Blacklistpurge.,$chan,.,$1),2)) {
    if ($BotSetting(BlacklistMax) = Purge) {
      Blacklistpurge $1
    }
    else {
      Blacklistshort $1
    }
  }
  else {
    if ($BotSetting(BlacklistMin) = Ban) {
      Blacklistban $1
    }
    elseif ($BotSetting(BlacklistMin) = Long) {
      Blacklistlong $1
    }
    elseif ($BotSetting(BlacklistMin) = Short) {
      Blacklistshort $1
    }
    else {
      Blacklistpurge $1
    }
  }
}

alias Blacklistpurge {
  set -u3600 $+(%,Blacklistpurge.,$chan,.,$1) ON
  message $chan /timeout $1 1
  if ($BotSetting(BlacklistVisible) = $null) {
    message $chan $iif($BotSetting(BlacklistMessage) != $null,$BotSetting(BlacklistMessage),Please do not use banned words!) ( $+ $user($1) $+ ) (Purge)
  }
}

alias Blacklistshort {
  set -u3600 $+(%,Blacklistshorttimeout.,$chan,.,$1) ON
  message $chan /timeout $1 $iif($BotSetting(BlacklistShort) != $null,$BotSetting(BlacklistShort),600)
  if ($BotSetting(BlacklistVisible) = $null) {
    message $chan $iif($BotSetting(BlacklistMessage) != $null,$BotSetting(BlacklistMessage),Please do not use banned words!) ( $+ $user($1) $+ ) (Short Timeout)
  }
}

alias Blacklistlong {
  set -u3600 $+(%,Blacklistlongtimeout.,$chan,.,$1) ON
  message $chan /timeout $1 $iif($BotSetting(BlacklistLong) != $null,$BotSetting(BlacklistLong),2880)
  if ($BotSetting(BlacklistVisible) = $null) {
    message $chan $iif($BotSetting(BlacklistMessage) != $null,$BotSetting(BlacklistMessage),Please do not use banned words!) ( $+ $user($1) $+ ) (Long Timeout)
  }
}

alias Blacklistwarning {
  set -u3600 $+(%,Blacklistbanwarning.,$chan,.,$1) ON
  message $chan /timeout $1 $iif($BotSetting(BlacklistLong) != $null,$BotSetting(BlacklistLong),2880)
  if ($BotSetting(BlacklistVisible) = $null) {
    message $chan $iif($BotSetting(BlacklistMessage) != $null,$BotSetting(BlacklistMessage),Please do not use banned words!) ( $+ $user($1) $+ ) (Long Timeout & Ban Warning)
  }
}

alias Blacklistban {
  set -u3600 $+(%,Blacklistban.,$chan,.,$1) ON
  message $chan /ban $1
  if ($BotSetting(BlacklistVisible) = $null) {
    message $chan $iif($BotSetting(BlacklistMessage) != $null,$BotSetting(BlacklistMessage),Please do not use banned words!) ( $+ $user($1) $+ ) (Ban)
  }
}
