; Script Name: SubWelcome.ini
; Type: Optional
; Credits - None
; You may edit this script.

on *:text:* subscribed to *:#:{
  if ($BotSetting(SubWelcome) = $null) {
    if ($nick = twitchnotify) {
      if ($6) {
        message $chan $replace($iif($BotSetting(HostReSubMessage) != $null,$BotSetting(HostReSubMessage),$user($1) subscribed to $4 for the $6 $+ th month!),^USER^,$user($1),^NICK^,$1,^MONTHS^,$6,^SUBCHAN^,$4,^CHAN^,$owner)
      }
      else {
        message $chan $replace($iif($BotSetting(HostSubMessage) != $null,$BotSetting(HostSubMessage),$user($1) subscribed to $4 $+ !),^USER^,$user($1),^NICK^,$1,^MONTHS^,$5,^SUBCHAN^,$4,^CHAN^,$owner)
      }
    }
  }
}

on *:text:*just subscribed*:#:{
  if ($nick = twitchnotify) {
    if ($4) {
      if ($BotSetting(SubWelcome) != New) || ($BotSetting(SubWelcome) = $null) {
        message $chan $replace($iif($BotSetting(ReSubMessage) != $null,$BotSetting(ReSubMessage),$user($1) subscribed for the $5 $+ th month!),^USER^,$user($1),^NICK^,$1,^MONTHS^,$5,^CHAN^,$owner)
      }
    }
    else {
      if ($BotSetting(SubWelcome) = On) || ($BotSetting(SubWelcome) = $null) {
        writeini -n BotSettings.ini $chan LastSub $1
        message $chan $replace($iif($BotSetting(SubMessage) != $null,$BotSetting(SubMessage),<3 <3 $user($1) just subscribed! <3 <3),^USER^,$user($1),^NICK^,$1,^MONTHS^,$5,^CHAN^,$owner)
        writeini -n BotSettings.ini $chan Subs $calc($BotSetting(Subs) + 1)
        if ($BotSetting(Subs) > $BotSetting(SubRecord)) {
          writeini -n BotSettings.ini $chan SubRecord $calc($BotSetting(SubRecord) + 1) 
        }
      }
    }
  }
}

on *:text:!set subwelcome *:#: {
  if ($access($nick) <= 400) {
    if ($3 = on) {
      writeini -n BotSettings.ini $chan SubWelcome On
      message $chan $user($nick) -> Sub-Welcome Enabled for this channel, including re-subscriptions.
    }
    if ($3 = host) {
      remini BotSettings.ini $chan SubWelcome 
      message $chan $user($nick) -> Sub-Welcome Enabled for this channel and hosted channels.
    }
    if ($3 = new) {
      writeini -n BotSettings.ini $chan SubWelcome New
      message $chan $user($nick) -> Sub-Welcome Enabled for New Users on this channel.
    }
    if ($3 = off) {
      writeini -n BotSettings.ini $chan SubWelcome Off
      message $chan $user($nick) -> Sub-Welcome Disabled.
    }
    if ($3 = hostmessage) {
      if ($4 = resub) {
        writeini -n BotSettings.ini $chan HostReSubMessage $5-
        message $chan $user($nick) -> The re-sub-welcome host message was updated.
      }
      if ($4 = first) {
        writeini -n BotSettings.ini $chan HostSubMessage $5-
        message $chan $user($nick) -> The sub-welcome host message was updated.
      }
    }
    if ($3 = chanmessage) {
      if ($4 = resub) {
        writeini -n BotSettings.ini $chan ReSubMessage $5-
        message $chan $user($nick) -> The re-sub-welcome message was updated.
      }
      if ($4 = first) {
        writeini -n BotSettings.ini $chan SubMessage $5-
        message $chan $user($nick) -> The sub-welcome message was updated.
      }
    }
  }
}
